import fr.avianey.androidsvgdrawable.gradle.SvgDrawableTask

import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: "androidsvgdrawable"
apply from: rootProject.file('global.gradle')

android {
    defaultConfig {
        applicationId "de.vanita5.twittnuker"
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 13
        versionName "0.7.0-beta"
        //multiDexEnabled false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    buildTypes {
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            SimpleDateFormat format = new SimpleDateFormat("yyyy-MM-dd")
            versionNameSuffix String.format(" (dev %s)", format.format(new Date()))
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            res.srcDirs = [project.file("src/$name/res"), project.file("src/$name/res-svg2png")]
        }
    }
}

configurations {
    all*.exclude group: 'org.yaml', module: 'snakeyaml'
}

repositories {
    jcenter()
    mavenCentral()
    maven {
        url "https://jitpack.io"
    }
    maven {
        url "https://repo.commonsware.com.s3.amazonaws.com"
    }
}

dependencies {
    apt 'com.bluelinelabs:logansquare-compiler:1.1.0'
    //compile 'com.android.support:multidex:1.0.1'
    compile 'com.android.support:support-v13:22.2.0'
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:cardview-v7:22.2.0'
    compile 'com.android.support:recyclerview-v7:22.2.0'
    compile 'com.sothree.slidinguppanel:library:3.0.0'
    compile 'com.twitter:twitter-text:1.12.1'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.4'
    compile 'org.apache.httpcomponents:httpclient-android:4.3.5'
    compile 'org.apache.httpcomponents:httpmime:4.3.5'
    compile 'com.squareup:otto:1.3.7'
    compile 'dnsjava:dnsjava:2.1.7'
    compile 'com.commonsware.cwac:merge:1.1.1'
    compile 'com.commonsware.cwac:sacklist:1.0.0'
    compile 'com.davemorrissey.labs:subsampling-scale-image-view:3.1.3'
    compile 'com.rengwuxian.materialedittext:library:2.1.3'
    compile 'com.pnikosis:materialish-progress:1.5'
    compile 'com.squareup.okhttp:okhttp:2.4.0'
    compile 'com.makeramen:roundedimageview:2.1.0'
    compile 'com.google.android.gms:play-services:7.5.0'
    compile 'com.google.maps.android:android-maps-utils:0.3.4'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'pl.droidsonroids.gif:android-gif-drawable:1.1.7'
    compile 'com.github.johnpersano:supertoasts:1.3.4.1@aar'
    compile 'com.github.mariotaku:MessageBubbleView:1.2'
    compile 'com.github.mariotaku:DragSortListView:0.6.1'
    compile 'com.github.mariotaku:SlidingMenu:1.3'
    compile 'com.github.uucky:ColorPicker-Android:0.9.7@aar'
    compile 'com.github.boxme:AsyncManager:9391ed71d7@aar'
    compile 'com.sprylab.android.texturevideoview:texturevideoview:1.0.0'
    compile 'com.bluelinelabs:logansquare:1.1.0'
    compile 'ch.acra:acra:4.6.2'
    compile 'org.jraf:android-switch-backport:2.0.1'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.4.4'
    compile project(':twittnuker.component.common')
    compile fileTree(dir: 'libs', include: ['*.jar'])
    debugCompile 'im.dino:dbinspector:3.1.0@aar'
    debugCompile 'com.facebook.stetho:stetho:1.1.1'
    debugCompile 'com.facebook.stetho:stetho-okhttp:1.1.1'
}

task svgToPng(type: SvgDrawableTask) {
    // specify where to pick SVG from
    from = file('src/main/svg-png')
    // specify the android res folder
    to = file('src/main/res-svg2png')
    // create qualified directories if missing
    createMissingDirectories = true
    // override files only if necessary
    overrideMode = 'ifModified'
    // let generate PNG for the following densities only
    targetedDensities = ['hdpi', 'mdpi', 'xhdpi', 'xxhdpi', 'xxxhdpi']
    // relative path of the file specifying nine patch specs
    ninePatchConfig = file('src/main/svg-png/9patch.json')
    // output format of the generated resources
    outputFormat = 'PNG'
}